plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'org.jreleaser' version '1.15.0'
    id 'org.sonarqube' version '6.0.1.5171'
}

repositories {
    mavenCentral()
}

dependencies { 
    implementation libs.springBootCore
    implementation libs.slf4jApi
    testImplementation libs.slf4jSimple
    testImplementation libs.junitJupiterEngine
    testImplementation libs.junitJupiterApi
    testImplementation libs.assertjCore
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Jar) {
    archiveBaseName.set(project.name)
    archiveVersion.set(project.version)
}

jar {
    manifest {
        attributes(
            'Build-Timestamp' : new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ"),
            'Build-Revision'  : project.version,
            'Built-By'        : 'Sysr',
            'Build-Tool'      : "Gradle ${gradle.gradleVersion}",
            'Build-Jdk'       : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
        )
    }

    archiveFileName.set("${project.name}-${project.version}.jar")
}

develocity {
    buildScan {
        termsOfUseUrl = "https://gradle.com/help/legal-terms-of-use"
        termsOfUseAgree = "yes"
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

sonar {
    properties {
        property "sonar.projectKey", prop("sonar_project_key")
        property "sonar.organization", prop("sonar_organization")
        property "sonar.token", prop("sonar_token")
        property "sonar.host.url", prop("sonar_host_url", "https://sonarcloud.io")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            description = project.description
            copyright = project.copyright
            inceptionYear = project.inceptionYear
            
            from components.java

            pom {
                name = project.name
                copyright = project.copyright
                description = project.description
                inceptionYear = project.inceptionYear

                url = 'https://github.com/sysrio/springcontext-env'

                licenses {
                    license {
                        name = 'Artistic License 2.0'
                        url = 'https://spdx.org/licenses/Artistic-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'calvnce'
                        name = 'Calvince Otieno'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/sysrio/springcontext-env.git'
                    developerConnection = 'scm:git:ssh://github.com/sysrio/springcontext-env.git'
                    url = 'http://github.com/sysrio/springcontext-env'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.projectDirectory.dir('release/staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            /* Portal Publisher API*/
            mavenCentral {
                sonatype {
                    active = 'NEVER'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('release/staging-deploy') 
                }
            }
        }
    }
    
    release {
        github {
            enabled = true
        }
    }

    gitRootSearch = true
    strict = true
}

def prop(name, defaultValue = null) {
    def value = project.findProperty(name) ?: System.getenv(name.toUpperCase()) ?: defaultValue
    if(value == null) {
        GradleException(
            """
            Property '${name}' is not set. Please specify it in gradle.properties or as an environment variable.
            """)
    }
    return value
}
